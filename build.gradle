plugins {
    id 'java'
    id 'java-library'
    id 'org.springframework.boot' version '3.3.4'
    id 'io.spring.dependency-management' version '1.1.6'
}

repositories {
    mavenCentral()
}

bootJar.enabled = false

subprojects {
    apply plugin: 'java'
    apply plugin: 'java-library'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'

    group = 'com.discphy'
    version = '0.0.1'
    sourceCompatibility = '21'

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    repositories {
        mavenCentral()
    }

    dependencies {
        implementation 'org.springframework.boot:spring-boot-starter'
        testImplementation 'org.springframework.boot:spring-boot-starter-test'
        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'
    }

    tasks.named('test') {
        useJUnitPlatform()
    }
}

project(':multi-core') {
    bootJar.enabled = false
    jar.enabled = true

    dependencies {
        api 'org.springframework.boot:spring-boot-starter-data-jpa'
        runtimeOnly 'com.h2database:h2'
    }
}

project(':multi-api') {
    bootJar.enabled = true
    jar.enabled = false

    dependencies {
        implementation project(':multi-core')
        implementation 'org.springframework.boot:spring-boot-starter-web'
    }
}